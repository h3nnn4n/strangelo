#version 460 core

layout (local_size_x = 32, local_size_y = 32, local_size_z = 1) in;

layout (rgba32f, binding = 0) uniform image2D imgOutput;
layout (location = 0) uniform float time;

layout (location = 1) uniform mat4 camera_view;
layout (location = 2) uniform bool orthographic;
layout (location = 5) uniform float near_plane;
layout (location = 6) uniform float far_plane;

#define N_SPHERES 10

struct sphere_t {
  vec3 position;
  float radius;
  vec3 color;
};


vec3 cast_ray(sphere_t spheres[N_SPHERES], vec3 ray_origin, vec3 ray_direction) {
  vec3 pixel_color = vec3(0.0, 0.0, 0.0);

  float hit_distance = far_plane;
  int sphere_hit_id = -1;
  vec3 normal = vec3(0.0, 0.0, 0.0);

  for (int i = 0; i < N_SPHERES; i++) {
    vec4 position = camera_view * vec4(spheres[i].position, 1.0);
    if (!orthographic) {
      position *= -1;
    }

    // Equation for ray hitting a sphere:
    // (ray_origin + ray_direction * t - position)^2 = radius^2
    // O = ray_origin
    // D = ray_direction
    // C = sphere center
    // omc = O - C
    // bsqmc = B^2 - C
    // iff bsqmc >= 0 then there is a hit
    vec3 omc = ray_origin - position.xyz;
    float b = dot(ray_direction, omc);
    float c = dot(omc, omc) - spheres[i].radius * spheres[i].radius;
    float bsqmc = b * b - c;

    if (bsqmc >= 0.0) {
      float t = -b - sqrt(bsqmc);
      if (t > near_plane && t < hit_distance) {
        hit_distance = t;
        sphere_hit_id = i;

        // Calculate the normal of the sphere at the hit point
        normal = normalize(ray_origin + ray_direction * t - position.xyz);
      }
    }
  }

  if (sphere_hit_id >= 0) {
    pixel_color.xyz = spheres[sphere_hit_id].color * dot(normal, (ray_origin - ray_direction));
  }

  return pixel_color;
}


void main() {
  vec4 pixel_color = vec4(0.0, 0.0, 0.0, 1.0);
  ivec2 pixel_position = ivec2(gl_GlobalInvocationID.xy);
  ivec2 texture_size = imageSize(imgOutput);

  float aspect_ratio = float(texture_size.x) / float(texture_size.y);
  float max_y = 5.0;
  float max_x = max_y * aspect_ratio;

  float r = 750;
  float f = 11.5;

  sphere_t spheres[N_SPHERES] = {
    //    position            radius  color
    {vec3(  0.0 ,-0.5 , -9.0) , 1.25 , vec3(1.0 , 0.0 , 0.0)} ,
    {vec3(  0.0 , 1.5 , -9.0) , 1.25 , vec3(0.0 , 0.0 , 1.0)} ,
    {vec3(  2.0 , 0.5 ,-11.0) , 2.5  , vec3(0.0 , 1.0 , 0.0)} ,
    {vec3(  0.0 , 0.0 ,-11.0) , 2.5  , vec3(0.3 , 0.8 , 0.2)} ,
    {vec3(  0.0 , 0.0 , 10.0) , 2.5  , vec3(0.4 , 0.2 , 0.8)} ,
    {vec3(  0.0 , 10.0,  0.0) , 2.5  , vec3(0.8 , 0.7 , 0.2)} ,
    {vec3(  0.0 ,-10.0,  0.0) , 2.5  , vec3(0.2 , 0.7 , 0.6)} ,
    {vec3( 10.0,  0.0 ,  0.0) , 2.5  , vec3(0.1 , 0.4 , 0.3)} ,
    {vec3(-10.0,  0.0 ,  0.0) , 2.5  , vec3(0.6 , 0.3 , 0.1)} ,
    {vec3(  0.0 , -r-f,  0.0) , r    , vec3(0.3 , 0.3 , 0.3)} ,
  };

  // Build a x,y in clip space (ie -1 to 1)
  float x = (float(pixel_position.x * 2 - texture_size.x) / texture_size.x);
  float y = (float(pixel_position.y * 2 - texture_size.y) / texture_size.y);

  // Build a ray from the camera position pointing forward (negative z)
  // Orthographic projection:
  vec3 ray_origin = vec3(x * max_x, y * max_y, 0.0);
  vec3 ray_direction = vec3(0.0, 0.0, -1.0);

  // Perspective projection:
  if (!orthographic) {
    ray_origin = vec3(0.0, 0.0, 0.0);
    ray_direction = normalize(ray_origin - vec3(x * max_x, y * max_y, -10.0));
  }

  float hit_distance = far_plane;
  int sphere_hit_id = -1;
  vec3 normal = vec3(0.0, 0.0, 0.0);

  pixel_color.rgb += cast_ray(spheres, ray_origin, ray_direction);

  // Gamma correction
  pixel_color.rgb = pow(pixel_color.rgb, vec3(1.0 / 2.2));

  imageStore(imgOutput, pixel_position, pixel_color);
}
